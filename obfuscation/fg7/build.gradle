plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[7.0,8.0)'
    id 'net.minecraftforge.renamer' version '1.0.1'
}

version = '0'
group = 'com.example'
base.archivesName = 'test'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    mappings channel: 'official', version: '1.21.10'
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
}

dependencies {
    implementation minecraft.dependency('net.minecraftforge:forge:1.21.10-60.0.17')
}

renamer.classes(tasks.named('jar', Jar)) {
    // This is generated by mavenizer, this is `net.minecraft:mappings_{channel}:{minecraft}-{mcp}:map2srg@tsrg.gz`
    // I intend to both publish these as public artifacts on the Forge maven, and expose a FG helper to make you not need to find the MCP version
    // Parchment is official mappings, just with parameters, so you can use official.
    mappings('net.minecraft:mappings_official:1.21.10-20251007.101210:map2srg@tsrg.gz')
}

publishing {
    repositories {
        maven { url = layout.projectDirectory.dir('repo').asFile.toURI() }
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java // Publish the normal jar
        artifact renameJar // Publish the renamed jar in addition
    }
}

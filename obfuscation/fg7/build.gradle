plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[7.0.9,8.0)'
    id 'net.minecraftforge.renamer' version '1.0.2'
}

version = '0.0'
group = 'com.example'
base.archivesName = 'obfuscation'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.1'
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
}

dependencies {
    implementation minecraft.dependency('net.minecraftforge:forge:1.20.1-47.4.0')
}

// Creates a task named 'renameJar'
renamer.classes(tasks.named('jar', Jar)) {
    // You need to point to the mappings you wish to apply, typically this is the Mapped names to SRG for older versions.
    // ForgeGradle/Mavenizer generate these files for the dependencies you declare. So you can use the helper.
    // Or you can specify the file or dependency if you host them yourself.
    map.from minecraft.dependency.toSrgFile
    // This is publishable task so you can specify things such as the classifier
    archiveClassifier = 'srg'
}

// If you want to create another task, or customize the name you can specify it as the first argument
renamer.classes('renameJarToSrg', tasks.named('jar', Jar)) {
    // This specifies the map via a dependency coordinate, such as 'net.minecraft:mappings_official:1.20.1-20230612.114412:map2srg@tsrg.gz'
    mappings(minecraft.dependency.toSrg.get())
}

publishing {
    repositories {
        maven { url = layout.projectDirectory.dir('repo').asFile.toURI() }
    }

    publications.register('mavenJava', MavenPublication) {
        from components.java // Publish the normal jar
        artifact renameJar // Publish the renamed jar in addition
    }
}

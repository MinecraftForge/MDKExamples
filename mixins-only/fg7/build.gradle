plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' //version '7.0.0-rc.2' // see settings.gradle
}

version = '1.0'
group = 'net.minecraftforge'
base.archivesName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    mappings channel: 'official', version: '1.21.11'
    runs {
        configureEach {
            workingDir.convention layout.projectDirectory.dir('run')
            // Mixin requires either specifying the config via command line, or in the Manifest
            args "--mixin.config=${mod_id}.mixins.json"
        }
        register('client') {
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }
    }
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    mavenCentral()
}

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:$minecraft_version-$forge_version")
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
}

//println project.configurations.compileClasspath.find { it.name.startsWith("forge-") }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.named('jar', Jar) {
    manifest {
        // Add our config to the manifest of our jar to have Mixin detect us at runtime.
        attributes['MixinConfigs'] = "${mod_id}.mixins.json"
    }
}

fgtools {
    configure('mavenizer') {
        classpath = configurations.detachedConfiguration(dependencies.create('net.minecraftforge:minecraft-mavenizer:0.4.19'))
    }
}

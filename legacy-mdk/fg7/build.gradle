plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' //version '7.0.0-rc.2' // see settings.gradle
    id 'net.minecraftforge.jarjar' version '0.2.3'
}

version = mod_version
group = mod_group_id
base.archivesName = mod_id

// Mojang ships Java 21 to end users in 1.20.5+, so your mod should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${providers.systemProperty('java.version').get()}, " +
    "JVM: ${providers.systemProperty('java.vm.version').get()} (${providers.systemProperty('java.vendor').get()}), " +
    "Arch: ${providers.systemProperty('os.arch').get()}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = true

    runs {
        configureEach {
            workingDir.convention layout.projectDirectory.dir('run')

            //systemProperty 'forge.logging.markers', 'REGISTRIES'

            systemProperty 'forge.logging.console.level', 'debug'

            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'

            //args "-mixin.config=${mod_id}.mixins.json"
        }

        register('client') {
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        register('server') {
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        register('gameTestServer') {
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }

        register('data') {
            workingDir = layout.projectDirectory.dir('run-data')

            args '--mod', mod_id, '--all', '--output', layout.projectDirectory.dir('src/generated/resources'), '--existing', layout.projectDirectory.dir('src/main/resources')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir layout.projectDirectory.dir('src/generated/resources') }

// This methods registers jarJar for the default jar task.
// The closure allows you to configure the task, instead of needing to do this:
// tasks.named('jarJar', net.minecraftforge.jarjar.gradle.JarJar)
jarJar.register() {
    archiveClassifier = null
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter {
            includeGroupAndSubgroups('org.spongepowered')
        }
    }
    mavenCentral()
    mavenLocal()

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html#sub:flat_dir_resolver
    // NOTE: Support for local dependencies is still experimental in ForgeGradle 7.
    //flatDir {
    //    dir 'libs'
    //}
}

dependencies {
    // Specify the version of Minecraft to use.
    // Any artifact can be supplied so long as it has a "userdev" classifier artifact and is a compatible patcher artifact.
    // The "userdev" classifier will be requested and setup by ForgeGradle.
    // If the group id is "net.minecraft" and the artifact id is one of ["client", "server", "joined"],
    // then special handling is done to allow a setup of a vanilla dependency without the use of an external repository.
    implementation minecraft.dependency("net.minecraftforge:forge:$minecraft_version-$forge_version")

    // Forge 1.21.6+ uses EventBus 7, which shifts most of its runtime validation to compile-time via an annotation processor
    // to improve performance in production environments. This line is required to enable said compile-time validation
    // in your development environment, helping you catch issues early.
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'

    // Example mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    //compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    //compileOnly "mezz.jei:jei-${mc_version}-forge-api:${jei_version}"
    //runtimeOnly "mezz.jei:jei-${mc_version}-forge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // NOTE: Support for deobfuscated dependencies has not yet been added in ForgeGradle 7.
    //implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
        mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.property('replaceProperties', replaceProperties)

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar) {
    manifest {
        attributes([
            'Specification-Title'     : mod_id,
            'Specification-Vendor'    : mod_authors,
            'Specification-Version'   : '1', // We are version 1 of ourselves
            'Implementation-Title'    : project.name,
            'Implementation-Version'  : project.jar.archiveVersion,
            'Implementation-Vendor'   : mod_authors
        ])
        //attributes['MixinConfigs'] = "${mod_id}.mixins.json"
    }

    archiveClassifier = 'slim'
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    repositories {
        maven { url = "file://${layout.projectDirectory.asFile}/mcmodsrepo" }
    }

    publications.register('mavenJava', MavenPublication) {
        // the java component publishes the jar output as the primary artifact
        //from components.java

        // the jarJar component publishes the jarJar output as the primary artifact
        from components.jarJar
    }
}

// IntelliJ no longer downloads javadocs and sources by default, this tells Gradle to force IntelliJ to do it.
idea.module { downloadJavadoc = downloadSources = true }

eclipse {
    // Eclipse no longer downloads javadocs and sources by default, this tells Gradle to force Eclipse to do it.
    classpath { downloadJavadoc = downloadSources = true }

    // NOTE: ForgeGradle 7 does not yet support Eclipse run configurations
    // Run everytime eclipse builds the code
    //autoBuildTasks genEclipseRuns
    // Run when importing the project
    //synchronizationTasks 'genEclipseRuns'
}

// NOTE: Merging of source sets is now controlled by the property 'net.minecraftforge.gradle.merge-source-sets'

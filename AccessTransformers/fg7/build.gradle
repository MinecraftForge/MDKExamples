plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    // AccessTransformers needs to be applied before FG if you want to use the minecraft block config options
    id 'net.minecraftforge.accesstransformers' version '5.0.2'
    id 'net.minecraftforge.gradle' version '7.0.0-beta.53'
}

version = '1.0'
group = 'net.minecraftforge'
base.archivesName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft {
    /*
     * This is a magic helper flag, it will enable access transformers on any
     * minecraft dependencies and look in all sourcesets for a file with
     * the path: META-INF/accesstransformer.cfg
     * It also enables some error messages when the file can not be found, to help users
     * who are using the most default case.
     */
    accessTransformers = true

    mappings channel: 'official', version: '1.21.11'
    runs {
        configureEach {
            workingDir.convention layout.projectDirectory.dir('run')
        }
        register('client')
    }
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    mavenCentral()
}

dependencies {
    // This will automatically have the transformer applied because we used minecraft.accessTransformers
    implementation minecraft.dependency("net.minecraftforge:forge:$minecraft_version-$forge_version")
    
    /*
     * If for some reason you do not wish to have the access transformer config in your sourcesets with the
     * default name. You can specify the transformer on specific dependencies directly.
     * Also note this allows you to transform any dependency, and have your config named anyting.
     * However, Forge only looks for META-INF/accesstransformers.cfg at runtime so you'll need to
     * make sure the file is named properly in your released jar. And that the targeted library is
     * loaded in the transformed layer.
     */
    implementation('net.minecraftforge:roimfs:1.0.0') {
        accessTransformers.configure(it) {
            config = project.file('access-changes.cfg')
        }
    }
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.named('jar', Jar) {
    manifest {
        // Add our config to the manifest of our jar to have Mixin detect us at runtime.
        attributes['MixinConfigs'] = "${mod_id}.mixins.json"
    }
}
